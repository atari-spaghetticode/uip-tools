Import('targetEnv')
Import('hostEnv')
Import('networkDevice')

import os.path

SOURCES_DEVICE_DRIVER=[];

if((networkDevice == 'netusbee') or (networkDevice == 'ethernec')):
    SOURCES_DEVICE_DRIVER = Glob('uip/netusbee/*.c')
elif(networkDevice == 'svethlana'):
    SOURCES_DEVICE_DRIVER = Glob('uip/svethlana/*.c') + targetEnv.Vasm('uip/svethlana/svethasm.s')
else:
    print("Unknown network device set. Valid options are: 'netusbee'/'ethernec' or 'svethlana'. Exiting...")
    SOURCES_DEVICE_DRIVER = ['']
    exit(0)

SOURCES = [
        "main.c",
        "uip_arch.c",
        "clock-arch.c",
        "ioredirect.c",
        targetEnv.Vasm('ioredirect-trap.s'),
        Glob('uip/*.c'),
        SOURCES_DEVICE_DRIVER,        
        targetEnv.SConscript("atari_http_server/SConscript", exports=['hostEnv', 'targetEnv'])
    ]

targetEnv.Append( CPPPATH=[ Dir('./uip').srcnode().abspath ] )
targetEnv.Append( CPPPATH=[ Dir('./').srcnode().abspath ] )

TARGETS =[
    targetEnv.Program("uip.tos", source = SOURCES),
    targetEnv.Program("romtest.tos", "romport-speed.c"),
    targetEnv.Program("contest.tos", "console-helper.c")
]

Return (["TARGETS"])